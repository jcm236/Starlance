buildscript {
	repositories {
		gradlePluginPortal()
		// These repositories are only for Gradle plugins, put any other repositories in the repository block further below
		maven {
			url 'https://repo.spongepowered.org/repository/maven-public/'
		}
	}
	dependencies {
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'

		// Make sure this version matches the one included in Kotlin for Forge
		classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.22'

	}
}

plugins {
	id 'maven-publish'
	id 'idea'

	id 'net.neoforged.moddev.legacyforge' version '2.0.137'

	// For auto-publishing
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'com.modrinth.minotaur' version '2.+'
}

// While _we_ don't use kotlin, this is still needed for VS to compile properly
// (It used to be included transitively, but now we don't use a transitive VS dep)
apply plugin: "org.jetbrains.kotlin.jvm"

apply plugin: 'maven-publish'
apply from: './gradle-scripts/publish-mod.gradle'

base {
	archivesName = "${archives_base_name}-${mod_version}"
}

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
}

legacyForge {
	version = "${minecraft_version}-${forge_version}"

	// Validate AT files and raise errors when they have invalid targets
	// This option is false by default, but turning it on is recommended
	validateAccessTransformers = true

	runs {
		client {
			client()
		}
		data {
			data()
		}
		server {
			server()
		}
	}

	mods {
		// Should this be vsch instead of testproject? I'm not sure
		testproject {
			sourceSet sourceSets.main
		}
	}
}

mixin {
	add sourceSets.main, "mixins.vsch.refmap.json"

	config "vsch.mixins.json"
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
	mavenCentral()
	mavenLocal()

	maven {
		url = "https://modmaven.dev/"
	}

	// Should mirror a lot of our deps, including Create, CC:T, KFF? and anything else VS has in its dev enviroment
	// However we still include the other mavens, just in case it goes down
	maven {
		name = "Valkyrien Skies Internal"
		url = "https://maven.valkyrienskies.org"
		content {
			excludeGroup("org.spongepowered")
		}
	}

	maven {
		name = "Modrinth Maven"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup("maven.modrinth")
		}
	}

	// Unfortunately curse maven is slow, but we need it for jade
	maven {
		name = "CurseForge Maven"
		url = "https://cursemaven.com"
		content {
			includeGroup("curse.maven")
		}
	}

	maven {
		name = "LiterMC maven"
		url = "https://litermc.github.io/maven/"
		content {
			includeGroupAndSubgroups("com.github.litermc")
		}
	}

	maven {
		name = "Create 0.5 maven"
		url = "https://maven.tterrag.com/"
		content {
			includeGroup("com.simibubi.create")
			includeGroup("com.jozufozu.flywheel")
			includeGroup("com.tterrag.registrate")
		}
	}

	maven {
		name = "Create 6.0 maven"
		url = "https://maven.createmod.net"
		content {
			includeGroup("com.simibubi.create")
			includeGroup("com.jozufozu.flywheel")
			includeGroup("com.tterrag.registrate")
		}
	}

	maven {
		name = "ParchmentMC"
		url = "https://maven.parchmentmc.org"
	}

	maven {
		name = "Kotlin for Forge"
		url = "https://thedarkcolour.github.io/KotlinForForge/"
//		content {
//			includeModule("thedarkcolour", "kotlinforforge")
//		}
	}

	maven {
		name = "Squiddev maven cct"
		url = "https://maven.squiddev.cc/"
		content {
			includeGroup("cc.tweaked")
			includeGroup("org.squiddev")
		}
	}

	maven {
		name = "Illusive Soulworks maven"
		url = "https://maven.theillusivec4.top/"
		content {
			includeGroup("top.theillusivec4.curios")
		}
	}
}

dependencies {
	// region mixins
	annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
	compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1"))
	jarJar(implementation("io.github.llamalad7:mixinextras-forge:0.4.1")) {
		version {
			strictly '[0.4.1,)'
			prefer '0.4.1'
		}
	}
	// endregion

	// region Valkyrien Skies
	implementation("org.valkyrienskies.core:api:${vs_core_version}") { transitive = false
		exclude group: 'org.joml', module: ''
	}
	implementation("org.valkyrienskies.core:internal:${vs_core_version}") { transitive = false
		exclude group: 'org.joml', module: ''
	}
	implementation("org.valkyrienskies.core:util:${vs_core_version}") { transitive = false
		exclude group: 'org.joml', module: ''
	}
	implementation("org.valkyrienskies.core:impl:${vs_core_version}") { transitive = false
		exclude group: 'org.joml', module: ''
	}
	modApi("org.valkyrienskies:valkyrienskies-120-forge:${vs2_version}") { transitive = false
		exclude group: 'org.valkyrienskies.core', module: ''
	}
	// endregion

	// region VS deps
	implementation "com.fasterxml.jackson.core:jackson-annotations:2.13.3"
	compileOnly("org.joml:joml:1.10.4")
	compileOnly("org.joml:joml-primitives:1.10.0")
	modImplementation "thedarkcolour:kotlinforforge:4.11.0"
	// endregion

	// region Personal deps (VUtil + CH)
	jarJar(modImplementation("com.github.litermc.vtil:vtil-forge-${minecraft_version}:${vtil_version}")) {
		version {
			strictly "[${vtil_version},)"
			prefer "${vtil_version}"
		}
	}
	modImplementation("maven.modrinth:cosmic-horizons-cosmos:${cosmos_version}")
	// endregion

	// region Compat deps

	// CC: Tweaked
	modCompileOnly("cc.tweaked:cc-tweaked-${minecraft_version}-forge:${cc_version}")

	// Jade
	modImplementation("curse.maven:jade-324717:${jade_version}")
	modRuntimeOnly("maven.modrinth:jade-addons-forge:5.3.1+forge")

	// Curios
	modCompileOnly("top.theillusivec4.curios:curios-forge:${curios_version}:api")

	// Create
	modImplementation("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }
	modImplementation("net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}")
	modCompileOnly("dev.engine-room.flywheel:flywheel-forge-api-${minecraft_version}:${flywheel_version}")
	modRuntimeOnly("dev.engine-room.flywheel:flywheel-forge-${minecraft_version}:${flywheel_version}")
	modImplementation("com.tterrag.registrate:Registrate:${registrate_version}")

	// JEI (just for quality of life, not compat)
	modRuntimeOnly("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

	// endregion
}

configurations {
	shade
	implementation.extendsFrom shade
}

processResources {
	var replaceProperties = [
		mod_id          : mod_id,
		mod_name        : mod_name,
		mod_license     : mod_license,
		mod_version     : mod_version,
		mod_authors     : mod_authors,
		mod_description : mod_description,

		minecraft_version       : minecraft_version,
		minecraft_version_range : minecraft_version_range,
		forge_version           : forge_version,
		forge_version_range     : forge_version_range,
		loader_version_range    : loader_version_range,

		// This property isn't used right now in mods.toml
		vs2_version: vs2_version.split("\\+").first(), // gradle.properties will have git commit version, but public releases won't
		cosmos_version: cosmos_version,
		cc_version: cc_version,
		create_version: create_version.split("-").first(), // Our version is 0.5.1j-55, but we support any 0.5.1j
	]

	inputs.properties replaceProperties

	filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
		expand replaceProperties + [project: project]
	}
}

jar {
	manifest {
		attributes([
			"Specification-Title": mod_id,
			"Specification-Vendor": mod_authors,
			"Specification-Version": "1",
			"Implementation-Title": project.name,
			"Implementation-Version": mod_version,
			"Implementation-Vendor": mod_authors,
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			"MixinConfigs": "vsch.mixins.json", // TODO: figure out why the MixinConfigs field did not add automatically
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/jcm236/Valkyrien-Skies-X-Cosmic-Horizons"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
}
